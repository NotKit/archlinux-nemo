# Maintainer: TheKit <nekit1000 at gmail.com>
_pkgname=libiodata
pkgname=libiodata-git
pkgver=0.19.9.r0.g7c2c027
pkgrel=1
pkgdesc="Mer library for input/output data"
arch=('i686' 'x86_64' 'aarch64')
url="https://git.merproject.org/mer-core/libiodata"
license=('GPL')
depends=('dbus')
makedepends=('git' 'bison' 'flex')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://git.merproject.org/mer-core/libiodata.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/$_pkgname"
	# Logging macro cause compilation to fail otherwise
    sed -i "s@define LOG_LEVEL LOG_WARNING@define LOG_LEVEL LOG_NONE@" src/log.h
}

build() {
	cd "$srcdir/$_pkgname"
    export IODATA_VERSION=0.19.9
    qmake
    make
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make INSTALL_ROOT="$pkgdir/" install
}
