# Maintainer: TheKit <nekit1000 at gmail.com>
pkgname=mce-git
pkgver=1.80.1.r0.gb3be273
pkgrel=1
pkgdesc="Mode Control Entity for Nokia mobile computers"
arch=('i686' 'x86_64' 'aarch64')
url="https://git.merproject.org/mer-core/mce"
license=('GPL')
makedepends=('git' 'doxygen' 'libngf' 'libdsme' 'libiphb' 'libsystemd')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://git.merproject.org/mer-core/mce.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
    sed -i "s|libsystemd-daemon|libsystemd|g" ./Makefile
}

build() {
	cd "$srcdir/${pkgname%-git}"
	make
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make DESTDIR="$pkgdir/" install
	
	mv $pkgdir/usr/sbin $pkgdir/usr/bin

	rm $pkgdir/lib/systemd/system/multi-user.target.wants/mce.service
	rmdir $pkgdir/lib/systemd/system/multi-user.target.wants
	mv $pkgdir/lib/systemd $pkgdir/usr/lib/systemd

	rm -rf $pkgdir/lib
	rm -rf $pkgdir/var/run
}
