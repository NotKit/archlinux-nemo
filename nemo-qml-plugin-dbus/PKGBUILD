# Maintainer: TheKit <nekit1000 at gmail.com>
pkgname=nemo-qml-plugin-dbus-git
pkgver=2.1.9.r0.g5d19af0
pkgrel=1
pkgdesc="DBus plugin for Nemo Mobile"
arch=('i686' 'x86_64' 'aarch64')
url="https://git.merproject.org/mer-core/nemo-qml-plugin-dbus"
license=('GPL')
depends=('qt5-base' 'nemo-keepalive')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://git.merproject.org/mer-core/nemo-qml-plugin-dbus.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/${pkgname%-git}"
	qmake
	make
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make INSTALL_ROOT="$pkgdir/" install
}
