# Maintainer: TheKit <nekit1000 at gmail.com>
pkgname=qtaround-git
pkgver=0.2.8.r0.g468b968
pkgrel=1
pkgdesc="QtAround library used to port the-vault to C++"
arch=('i686' 'x86_64' 'aarch64')
url="https://git.merproject.org/mer-core/qtaround"
license=('GPL')
depends=('qt5-base')
makedepends=('git' 'cor' 'tut')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://git.merproject.org/mer-core/qtaround.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
    sed -i "s|tut>=0.0.3|tut>=0.0.0|g" ./tests/CMakeLists.txt
    sed -i "s|#include <QMap>|#include <QMap>\n#include <cmath>|g" ./src/util.cpp
}

build() {
	cd "$srcdir/${pkgname%-git}"
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DVERSION=$pkgver
	make
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make DESTDIR="$pkgdir/" install
}
