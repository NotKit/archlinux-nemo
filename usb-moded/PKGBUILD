# Maintainer: TheKit <nekit1000 at gmail.com>
_pkgname=usb-moded
pkgname=usb-moded-git
pkgver=0.86.0+mer14.r0.geefc64c
pkgrel=1
pkgdesc="Mer USB mode controller"
arch=('i686' 'x86_64' 'aarch64')
url="https://git.merproject.org/mer-core/usb-moded"
license=('GPL')
depends=('dbus')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://git.merproject.org/mer-core/usb-moded.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --long --tags | sed 's/^mer\///;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/$_pkgname"
    ./autogen.sh
    ./configure --prefix=/usr
    make
}

package() {
	cd "$srcdir/$_pkgname"
	make DESTDIR="$pkgdir/" install
	mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"

    install -m 644 -D src/usb_moded-dbus.h "$pkgdir/usr/include/$_pkgname/usb_moded-dbus.h"
    install -m 644 -D src/usb_moded-modes.h "$pkgdir/usr/include/$_pkgname/usb_moded-modes.h"
    install -m 644 -D src/usb_moded-appsync-dbus.h "$pkgdir/usr/include/$_pkgname/usb_moded-appsync-dbus.h"
    install -m 644 -D src/com.meego.usb_moded.xml "$pkgdir/usr/include/$_pkgname/com.meego.usb_moded.xml"
    install -m 644 -D usb_moded.pc "$pkgdir/usr/lib/pkgconfig/usb_moded.pc"
	
}
